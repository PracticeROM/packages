# Maintainer: glank <glank@practicerom.com>

_commit="be7ccd0"
_binutils_ver=2.45
_gcc_ver=15.2.0
_newlib_ver=4.5.0.20241231
_gdb_ver=16.3

pkgbase="n64-ultra"
pkgname=(
	"mips64-ultra-elf-binutils"
	"mips64-ultra-elf-gcc"
	"mips64-ultra-elf-gcc-libs"
	"mips64-ultra-elf-newlib"
	"mips64-ultra-elf-gdb"
	"mips64-ultra-elf-practicerom-libs"
	"practicerom-tools"
)
pkgver=0
pkgrel=1
arch=("x86_64")
url="https://github.com/glankk/n64"
groups=("practicerom-dev")
makedepends=(
	"curl" "git" "tar"
	"jansson" "libusb" "lua" "zlib"
	"expat" "guile" "ncurses" "python" "xxhash" "xz"
)
options=("!debug")
source=("n64::git+${url}.git#commit=${_commit}")
sha512sums=("SKIP")

pkgver() {
	rev=$(git -C n64 rev-list --count "${_commit}")
	printf "r%s.%s" "${rev}" "${_commit}"
}

prepare() {
	cd n64 && ./configure \
		--prefix=/usr \
		--with-configure-toolchain="--libexecdir='\${exec_prefix}/lib'" \
		BINUTILS_VERSION="binutils-${_binutils_ver}" \
		GCC_VERSION="gcc-${_gcc_ver}" \
		NEWLIB_VERSION="newlib-${_newlib_ver}" \
		GDB_VERSION="gdb-${_gdb_ver}" \
		CFLAGS="${CFLAGS} -Wno-error=format-security" \
		CXXFLAGS="${CXXFLAGS} -Wno-error=format-security" \
		LDFLAGS="${LDFLAGS}"
}

build() {
	CFLAGS="${CFLAGS} -Wno-error=format-security"
	CXXFLAGS="${CXXFLAGS} -Wno-error=format-security"

	make -C n64 all
	make -C n64 toolchain-all
}

package_mips64-ultra-elf-binutils() {
	pkgdesc="GNU Binutils targeting mips64-ultra-elf"
	license=("GPL")
	depends=(
		"gcc-libs"
		"glibc"
		"libelf"
		"zstd"
	)
	options=("staticlibs" "!emptydirs")

	make DESTDIR="${pkgdir}" -C n64 toolchain-install-strip-binutils
	make DESTDIR="${pkgdir}" -C n64 toolchain-install-strip-gas
	make DESTDIR="${pkgdir}" -C n64 toolchain-install-strip-ld

	rm -r "${pkgdir}"/usr/lib/ # bfd-plugins/libdep.so
	rm -r "${pkgdir}"/usr/share/info/
}

package_mips64-ultra-elf-gcc() {
	pkgdesc="GCC targeting mips64-ultra-elf"
	license=("GPL-3.0-with-GCC-exception" "GFDL-1.3-or-later")
	depends=(
		"gcc-libs"
		"glibc"
		"zstd"
		"mips64-ultra-elf-binutils"
		"mips64-ultra-elf-gcc-libs=${pkgver}-${pkgrel}"
		"mips64-ultra-elf-newlib=${pkgver}-${pkgrel}"
	)
	options=("staticlibs" "!emptydirs")

	make DESTDIR="${pkgdir}" -C n64 toolchain-install-strip-gcc \
		INSTALL_HEADERS=install-headers
	make DESTDIR="${pkgdir}" -C n64 toolchain-install-strip-c++tools
	make DESTDIR="${pkgdir}" -C n64 toolchain-install-strip-lto-plugin
	make DESTDIR="${pkgdir}" -C n64 toolchain-install-strip-libcc1

	rm "${pkgdir}"/usr/lib/libcc1*
	rm -r "${pkgdir}"/usr/share/info/
	rm -r "${pkgdir}"/usr/share/man/man3/
	rm -r "${pkgdir}"/usr/share/man/man7/
}

package_mips64-ultra-elf-gcc-libs() {
	pkgdesc="Libraries for GCC targeting mips64-ultra-elf"
	arch=("any")
	license=("GPL-3.0-with-GCC-exception" "GFDL-1.3-or-later")
	options=("!strip" "staticlibs" "!emptydirs")

	make DESTDIR="${pkgdir}" -C n64 toolchain-install-target-libstdc++-v3
	make DESTDIR="${pkgdir}" -C n64 toolchain-install-target-libssp
	make DESTDIR="${pkgdir}" -C n64 toolchain-install-target-libgcc

	rm -r "${pkgdir}"/usr/share/ # gcc-${_gcc_ver}/python/libstdcxx/
	rm "${pkgdir}"/usr/lib/gcc/mips64-ultra-elf/"${_gcc_ver}"/include/unwind.h
}

package_mips64-ultra-elf-newlib() {
	pkgdesc="Newlib targeting mips64-ultra-elf"
	arch=("any")
	license=("BSD")
	options=("!strip" "staticlibs" "!emptydirs")

	make DESTDIR="${pkgdir}" -C n64 toolchain-install-target-newlib
	make DESTDIR="${pkgdir}" -C n64 toolchain-install-target-libgloss

	rm -r "${pkgdir}"/usr/share/
}

package_mips64-ultra-elf-gdb() {
	pkgdesc="GDB targeting mips64-ultra-elf"
	license=("GPL-3.0-or-later" "LGPL-3.0-or-later")
	depends=(
		"bash"
		"expat"
		"gcc-libs"
		"gdb-common=${_gdb_ver}"
		"glibc"
		"guile"
		"libelf"
		"ncurses"
		"python"
		"zstd"
		"xxhash"
		"xz"
	)
	options=("staticlibs" "!emptydirs")

	make DESTDIR="${pkgdir}" -C n64 toolchain-install-strip-gdb
	make DESTDIR="${pkgdir}" -C n64 toolchain-install-strip-sim

	rm -r "${pkgdir}"/usr/include/
	rm -r "${pkgdir}"/usr/share/gdb/
	rm -r "${pkgdir}"/usr/share/info/
}

package_mips64-ultra-elf-practicerom-libs() {
	pkgdesc="Practicerom development headers and libraries"
	arch=("any")
	options=("!strip" "staticlibs" "!emptydirs")

	make DESTDIR="${pkgdir}" -C n64 install-sys
}

package_practicerom-tools() {
	pkgdesc="Practicerom development tools"
	depends=(
		"gcc-libs"
		"glibc"
		"jansson"
		"libusb"
		"lua"
		"zlib"
	)
	options=("staticlibs" "!emptydirs")

	make DESTDIR="${pkgdir}" -C n64 install-strip

	mv "${pkgdir}"/usr/bin/gs "${pkgdir}"/usr/bin/n64-gs
}
